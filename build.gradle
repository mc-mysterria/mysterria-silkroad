plugins {
    id 'java'
    id('com.gradleup.shadow') version '8.3.2'
    id('xyz.jpenilla.run-paper') version "2.3.1"
}

group = 'net.mysterria.silkroad'
version = '1.0.1'

repositories {
    mavenCentral()
    maven {
        url = "https://repo.triumphteam.dev/snapshots/"
    }

    flatDir {
        dirs("libs")
    }

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url "https://repo.panda-lang.org/releases"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    implementation("dev.triumphteam:triumph-gui-paper:3.1.13-SNAPSHOT")
    implementation 'org.yaml:snakeyaml:2.0'
    implementation("dev.rollczi:litecommands-bukkit:3.10.4")
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add("-parameters")
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

shadowJar {
    relocate 'dev.rollczi.litecommands', 'net.mysterria.silkroad.litecommands'
}

assemble.dependsOn shadowJar

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks {
    runServer {
        minecraftVersion("1.21.8")
    }
}